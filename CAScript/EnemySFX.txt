Class DCY_GeneralComet : DCY_Projectile
{
  Default
  {
  Radius 12;
  Height 12;
  Speed 15;
  Damage 2;
  Scale 0.85;
  RenderStyle "Normal";
  Alpha 1.0;
  +ROLLSPRITE
  +NEVERFAST
  +EXTREMEDEATH
  Projectile;
  SeeSound "DCYComet/Spawn";
  DeathSound "DCYComet/Explode";
  Decal "Scorch";
  }
  
  float r;
  bool died;
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	r = frandom(0.8, 4.0) * randompick(1, -1);
  }
  
  override void Tick()
  {
	super.Tick();
	
	if (died) return;
	if (vel ~== (0, 0, 0))
	{
		if (DCY_EffectDistance(900))
			for (int i = random(5, 10); i > 0; i--)
				A_SpawnItemEx("DCY_CometChunk", frandom(self.radius, -self.radius), frandom(self.radius, -self.radius), frandom(self.height, -self.height), frandom(-7, 7), frandom(-7, 7), frandom(-1, 10), frandom(0, 360));
		died = true;
	}
	else
	{
		roll += r;
		
		if ((level.time % random(1, 3)) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 100, 80, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 1), startalphaf: 0.25, fadestepf: 0.0014, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
	}
  }
  
  States
  {
  Spawn:
    CGMT AABBCC 1 Bright A_SpawnItemEx("DCY_CometTrail", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), vel.x / 4, vel.y / 4, vel.z / 4, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY);
    Loop;
  Death:
	CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_Explode(100, 100, XF_NOTMISSILE, 1);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN, falloff: 400);
	}
    CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_SetTranslucent(1.0, 1);
		
		let splode = spawn("DCY_MonochromaticExplosion", pos);
		if (splode) splode.scale *= 1.2;
		
		for (int i = 3; i > 0; i--) A_SpawnItemEx("DCY_MonochromaticExplosion", 0, 0, 0, random(-2, 2), random(-2, 2), random(-2, 2), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    CGMT EEFF 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
	}
	CGMT GGHHII 1 Bright A_SetScale(Scale.X+0.25, Scale.Y+0.25);
	TNT1 A 25;
    Stop;
  }
}

Class DCY_CometTrail : DCY_Effect
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Alpha 0.5;
  Scale 0.25;
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  +ROLLSPRITE
  }
  
  override void PostBeginPlay() { super.PostBeginPlay(); roll = frandom(0, 360); }
  
  States
  {
  Spawn:
	_2WM ABCDEFGHIJKLMNO 1 A_SetScale(Scale.X-0.005, Scale.Y-0.005);
	Stop;
  }
}

Class DCY_CometChunk : DCY_GoreBase
{
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	
	frame = random(0, 4);
	scale.x *= randompick(-1, 1);
	scale.y *= randompick(-1, 1);
  }
  
  override void Tick()
  {
	super.Tick();
	if (vel.z == 0 && (GetAge() > (dcy_gibtime / 3)))
	{
		A_FadeOut(0.01);
		if (alpha <= 0) Destroy();
	}
  }
  
  States
  {
  Spawn:
	CHMT # 1 { roll += xroll; }
	Loop;
  Death:
  Crash:
	CHMT # -1;
	Stop;
  }
}

Class DCY_FatThrower : DCY_Projectile
{
  Default
  {
  Speed 32;
  FastSpeed 35;
  DamageFunction (3);
  Radius 18;
  Height 18;
  Scale 0.05;
  Alpha 1;
  DamageType "Fire";
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[1.68,0.80,0.34]"; //"0:255=%[0.00,0.00,0.00]:[1.50,0.31,0.00]";
  Projectile;
  +NEVERFAST;
  +RIPPER;
  +NOEXTREMEDEATH;
  +ROLLSPRITE;
  }
  
  override void PostBeginPlay() { super.PostBeginPlay(); roll = frandom(0, 360); }
  
  States
  {
  Spawn:
	_2WM ABCDEFGHJIKLMNO 1 Bright
	{
		scale += (0.03, 0.03);
		if (DCY_EffectDistance(sfxdistance))
		{
			let f = spawn("DCY_CometTrail", pos + (random(-5, 5), random(-5, 5), random(-5, 5)));
			if (f)
			{
				f.scale = scale;
				f.alpha /= 2.5;
				f.vel = vel / frandom(2, 8);
				f.vel.x += frandom(-0.1, 0.1);
				f.vel.y += frandom(-0.1, 0.1);
				f.vel.z += frandom(-0.1, 0.1);
				f.translation = translation;
			}
		}
	}
	Goto Death;
  Death:
    TNT1 A 1 A_Die;
	Stop;
  }
}

Class DCY_RattailFire : DCY_FatThrower
{
  Default { Translation "0:255=%[0.00,0.00,0.00]:[0.38,0.80,1.66]"; DamageFunction (7); }
  
  States
  {
  Spawn:
	_2WM ABCDEFGHJIKLMNO 1 Bright
	{
		scale += (0.02, 0.02);
		if (DCY_EffectDistance(sfxdistance))
		{
			let f = spawn("DCY_CometTrail", pos + (random(-5, 5), random(-5, 5), random(-5, 5)));
			if (f)
			{
				f.scale = scale;
				f.alpha /= 1.1;
				f.vel = vel / frandom(2, 8);
				f.vel.x += frandom(-0.1, 0.1);
				f.vel.y += frandom(-0.1, 0.1);
				f.vel.z += frandom(-0.1, 0.1);
				f.translation = translation;
			}
		}
		
		vel.z += 0.1;
	}
	Goto Death;
  }
}

Class DCY_ThrowerEffect : DCY_Effect
{
  Default
  {
  Radius 0;
  Height 0;
  Scale 0.25;
  Alpha 0.035;
  RenderStyle "Add";
  +NOCLIP
  +NOGRAVITY
  +THRUACTORS
  +BRIGHT
  }
  States
  {
  Spawn:
    _EWM ABCDEFGHJIKLMNO 1 Bright;
	Stop;
  }
}

Class DCY_WretchedthrowerEffect : DCY_ThrowerEffect { Default { Alpha 0.1; Scale 0.375; } }

Class DCY_WretchedFlame : DCY_FatThrower
{
  Default
  {
  Translation "0:255=%[0.20,0.00,0.00]:[2.00,0.03,0.03]";
  DamageType "WretchedFire";
  Alpha 0.1;
  }
  States
  {
  Spawn:
	_2WM ABCDEFGHJIKLMNO 1 Bright
	{
		A_Explode(2, 35, 0, 1);
		scale += (0.02, 0.02);
		
		if (DCY_EffectDistance(sfxdistance))
		{
			let f = spawn("DCY_CometTrail", pos + (random(-5, 5), random(-5, 5), random(-5, 5)));
			if (f)
			{
				f.scale = scale;
				f.alpha /= 1.1;
				f.vel = vel / frandom(2, 8);
				f.vel.x += frandom(-0.1, 0.1);
				f.vel.y += frandom(-0.1, 0.1);
				f.vel.z += frandom(-0.1, 0.1);
				f.translation = translation;
			}
			
			A_SpawnItemEx("DCY_ElecDeathBase", random(-30, 30), random(-30, 30), random(-30, 30), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 120);
		}
		
		vel.z += 0.235;
	}
	Goto Death;
  }
}

Class DCY_WastelandFlame : DCY_FatThrower
{
  Default
  {
  DamageType "Fire";
  Alpha 0.1;
  DamageFunction (6);
  }
  
  States
  {
  Spawn:
	_EWM ABCDEFGHJIKLMNO 1 Bright
	{
		scale += (0.04, 0.04);
		if (DCY_EffectDistance(sfxdistance))
		{
			let f = spawn("DCY_CometTrail", pos + (random(-5, 5), random(-5, 5), random(-5, 5)));
			if (f)
			{
				f.scale = scale;
				f.alpha /= 1.1;
				f.vel = vel / frandom(5, 12);
				f.vel.x += frandom(-0.1, 0.1);
				f.vel.y += frandom(-0.1, 0.1);
				f.vel.z += frandom(-0.1, 0.1);
				f.translation = translation;
			}
		}
		
		vel.z += 0.1;
	}
	Stop;
  Death:
	TNT1 A 1 { if (!random(0, 1)) A_SpawnItemEx((!random(0, 3)) ? "DCY_NapalmBigFire" : "DCY_NapalmFire", frandom(-20, 20), frandom(-20, 20)); }
	Stop;
  }
}

Class DCY_OverdemoniacExplosion : Actor
{
  Default
  {
  Scale 1.0;
  +ROLLSPRITE
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  +NOBLOCKMAP
  Radius 0;
  Height 0;
  Alpha 1.0;
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  }
  States
  {
  Spawn:
	SUPH A 0 NoDelay A_SetRoll(Roll+random(0, 360));
	SUPH ABCDEFGHIJKLMNNOOPPQQRR 1;
	Stop;
  }
}

Class DCY_ElecTrailBase : DCY_Effect
{
  Default
  {
  +NOGRAVITY
  +BRIGHT
  +THRUACTORS
  +NOCLIP
  +ROLLSPRITE
  +NOINTERACTION
  -SOLID
  RenderStyle "Add";
  Alpha 1.0;
  Scale 1.0;
  }
  
  States
  {
  Spawn:
	ASZ0 ABCABCABC 1
	{
		A_SetScale(Scale.X - 0.01);
		A_FadeOut(0.1);
		A_SetRoll(frandom(0, 360));
	}
	Stop;
  }
}

Class DCY_ElecDeathBase : DCY_ElecTrailBase
{
  States
  {
  Spawn:
	4L4C A 0 NoDelay { roll = frandom(0, 360); }
	4L4C ABCDEFG 2;
	Stop;
  }
}

Class DCY_FallingSpark : DCY_Effect
{
  Mixin DCY_Effects;
  
  Default
  {
	Radius 2;
	Height 4;
	BounceFactor 0.55;
	WallBounceFactor 0.55;
	Gravity 0.8;
	Speed 9;
	Mass 2;
	BounceType "Doom";
	+NOBLOCKMAP
	+MISSILE
	+DONTSPLASH
	+NOTELEPORT
	DCY_FallingSpark.SColor "8a3919";
  }
  
  string col;
  property SColor : col;
  protected textureID t;
  double f;
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	f = random(2, 100);
  }
  
  States
  {
  Spawn:
	TNT1 A random(1, 2)
	{
		if (f < 0) Destroy();
		f--;
		
		if (DCY_EffectDistance(900))
			A_SpawnParticleEx(col, t, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 90, 7, frandom(0, 360), velx: frandom(-1, 1) + (vel.x / 5), vely: frandom(-1, 1)+ (vel.y / 5), velz: frandom(-0.1, 0.1) + (vel.z / 5), accelz: -0.05, startalphaf: 1, fadestepf: 0, sizestep: -0.2);
	}
	Loop;
  Death:
	TNT1 A 0;
	Stop;
  }
}

Class DCY_BigSpark : DCY_FallingSpark
{
  Default
  {
  Speed 8;
  Gravity 0.5;
  }
  
  override void PostBeginPlay()
  {
	DCY_Effect.PostBeginPlay();
	f = random(2, 10);
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (f < 0) Destroy();
		f--;
		
		if (DCY_EffectDistance(900))
			for (int i = random(1, 6); i > 0; i--) A_SpawnParticleEx(col, t, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 90, 10, frandom(0, 360), velx: frandom(-2.5, 2.5) + (vel.x / 2), vely: frandom(-2.5, 2.5) + (vel.y / 2), velz: frandom(-2.1, 2.1) + (vel.z / 2), accelz: -0.1, startalphaf: 1, fadestepf: 0, sizestep: -0.3);
	}
	Loop;
  }
}

Class DCY_LightningExplode : DCY_Explosion_1
{
  Mixin DCY_Effects;
  
  Default
  {
	Scale 0.5;
	Translation "0:255=%[0.00,0.00,0.00]:[1.39,1.50,1.72]";
	+ALWAYSPUFF
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	
	if (DCY_EffectDistance(900))
	{
		for (int i = random(2, 5); i > 0; i -= random(1, 2))
		{
			DCY_BigSpark s = DCY_BigSpark(Spawn("DCY_BigSpark", pos + (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10))));
			if (s) s.vel = (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10));
		}
	}
  }
  
  override void Tick()
  {
	if (level.isFrozen()) return;
	super.Tick();
	
	if (DCY_EffectDistance(900) && (getAge() < 15))
	{
		if (!random(0, 1))
		{
			for (int i = random(1, 2); i > 0; i--)
			{
				DCY_FallingSpark s = DCY_FallingSpark(Spawn("DCY_FallingSpark", pos + (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10))));
				if (s) s.vel = (frandom(-14, 14), frandom(-14, 14), frandom(-14, 14));
			}
		}
		
		A_SpawnItemEx("DCY_SiriusArc", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
	}
  }
}

Class DCY_SmallBFG10kBoom : BulletPuff
{
  Default
  {
	  Radius 0;
	  Height 0;
	  Scale 0.76;
	  Alpha 1.0;
	  VSpeed 0;
	  +ALWAYSPUFF;
	  RenderStyle "Add";
	  +ROLLSPRITE;
	  +FLATSPRITE;
	  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.49,0.72]";
	  +BRIGHT;
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	
	A_SetRoll(Roll+random(0, 360));
	A_SetAngle(Angle+random(0, 360));
	A_SetPitch(Pitch+random(0, 360));
		
	for (int i = 3; i > 0; i--)
	{
		A_SpawnItemEx("DCY_BFG10kLaserPuffSpark", 0, 0, 0, frandom(10.0, 20.5), 0, frandom(2.0, 13.0)+frandom(5.0, -5.0), frandom(-360.0, 360.0)+frandom(-180.0, 180.0), SXF_NOCHECKPOSITION, 28);
		A_SpawnItemEx("DCY_BFG10kLaserPuffSpark", 0, 0, 0, frandom(10.0, 20.5), 0, frandom(2.0, 13.0)+frandom(5.0, -5.0), frandom(-360.0, 360.0)+frandom(-180.0, 180.0), SXF_NOCHECKPOSITION, 60);
	}
		
	A_Explode(random(5, 10), 100, XF_NOTMISSILE);
  }
  
  States
  {
  Spawn:
	SUPH AACEGIKMOPQR 1;
	Stop;
  }
}

Class DCY_ApollyonFireTrail : DCY_Effect
{
  Default
  {
	RenderStyle "Add";
	Alpha 2;
	Scale 1;
	+NOINTERACTION
	-SOLID
	+NOBLOCKMAP
	Radius 0;
	Height 0;
  }
  
  States
  {
  Spawn:
	123E A 0;
	123E # 1
	{
		A_FadeOut(0.15);
		scale *= 0.9;
	}
	Wait;
  }
}