Class DCY_Apollyon : DCY_VileParent
{
  Default
  {
  Tag "Apollyon";
  BloodColor "Black";
  Speed 10;
  FastSpeed 14;
  +MISSILEMORE
  +FIRERESIST
  +QUICKTORETALIATE
  +DONTHARMCLASS
  Translation "0:255=%[0.00,0.00,0.00]:[0.32,0.32,0.32]", "168:191=[94,85,85]:[0,0,0]", "16:47=[116,63,63]:[0,0,0]", "208:235=%[0.18,0.00,0.00]:[2.00,1.35,0.27]", "248:255=%[0.18,0.00,0.00]:[2.00,0.85,0.00]", "4:4=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "0:3=%[0.00,0.00,0.00]:[0.50,0.00,0.00]", "49:49=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "160:167=%[0.00,0.00,0.00]:[2.00,2.00,2.00]";
  DamageFactor "Ice", 1.5;
  SeeSound "Apollyon/Sight";
  ActiveSound "Apollyon/Active";
  PainSound "Apollyon/Pain";
  DeathSound "Apollyon/Death";
  Obituary "%o got turned into scarlet dust by Apollyon.";
  HitObituary "%o got turned into scarlet dust by Apollyon.";
  MeleeThreshold 200;
  DCYMonster.ScientificName "Magus immolationis";
  }
  
  int cooldown;
  bool firebuff;
  bool d;
  
  static const string pillars[] =
  {
	"DCY_ApollyonPillar",
	"DCY_ApollyonPillar2",
	"DCY_ApollyonPillar3",
	"DCY_ApollyonPillar4"
  };
  
  override void Tick()
  {
	Super.Tick();
	
	If (Health > 0 && !IsFrozen() && DCY_EffectDistance(sfxdistance) && !(sprite == GetSpriteIndex("TNT1A0")))
	{
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(0xef5501, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(3, 9), frandom(0, 360), frandom(-self.radius, self.radius), frandom(-self.radius, self.radius), frandom(2, self.height), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.05, 0.2), fadestepf: 0, sizestep: -0.15);
		if ((level.time % random(1, 7)) == 0)
			A_SpawnParticleEx(0x222222, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 90, random(80, 120), 0, frandom(-self.radius, self.radius), frandom(-self.radius, self.radius), frandom(2, self.height), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.3, 1), startalphaf: 0.08, fadestepf: 0.0007, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
	}
  }
  
  int teleloop, telecool;
  
  States
  {
  Spawn:
	A_PB A 10 DCY_Look();
	Loop;
  Look:
	#### AABBCCDDEEFF 4 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	A_PB AABBCCDDEEFF 4
	{
		DCY_Chase("FireBlast", "Missile");
		if (target && CheckSight(target) && !random(0, 80) && telecool <= 0) SetStateLabel("FireTeleport");
		if (target && distance3D(target) > 300)
		{
			tics = 2;
			A_FaceTarget(5, 5);
			A_Recoil(-0.15);
			if (DCY_EffectDistance(sfxdistance))
				A_SpawnParticleEx(0xff5219, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 60, random(70, 120), angle, frandom(-self.radius, self.radius), frandom(-self.radius, self.radius), frandom(2, self.height), velz: frandom(0, 2.5), startalphaf: 0.1, fadestepf: 0.0025, sizestep: -2, startroll: frandom(0, 360), rollvel: random(0, 1) ? 2 : -2);
		}
		
		if (telecool > 0) telecool--;
	}
	Loop;
  FireTeleport:
	A_PB A 0
	{
		teleloop = random(20, 30);
	}
	A_PB AJKLKL 2;
	TNT1 A 0
	{
		A_StartSound("Apollyon/Teleport", 20, attenuation: ATTN_IDLE);
		A_QuakeEx(2, 2, 2, 40, 0, 800, "", QF_SCALEDOWN|QF_3D, rollintensity: 1);
		for (int i = 5; i > 0; i--) A_SpawnItemEx("DCY_Explosion_1", frandom(-self.radius, self.radius) / 4, frandom(-self.radius, self.radius) / 4, (frandom(self.height + 10, self.height - 10)) / 2, frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), flags: SXF_NOCHECKPOSITION);
	}
	Goto FireLoop;
  FireLoop:
	TNT1 A 0
	{
		A_Recoil(-1);
		for (int i = random(1, 2); i > 0; i--) A_Chase(null, null, CHF_NOPLAYACTIVE);
		
		telecool += random(2, 3);
		
		if (DCY_EffectDistance(sfxdistance))
			A_SpawnParticleEx(0xff5219, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 60, random(70, 120), angle, frandom(-self.radius, self.radius) / 4, frandom(-self.radius, self.radius) / 4, (frandom(self.height + 10, self.height - 10)) / 2, velz: frandom(0, 2.5), startalphaf: 1, fadestepf: 0.035, sizestep: -1, startroll: frandom(0, 360), rollvel: random(0, 1) ? 2 : -2);
	}
	TNT1 A 0
	{
		A_Recoil(-5);
		if (DCY_EffectDistance(sfxdistance))
			A_SpawnParticleEx(0xff5219, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 60, random(70, 120), angle, frandom(-self.radius, self.radius) / 4, frandom(-self.radius, self.radius) / 4, (frandom(self.height + 10, self.height - 10)) / 2, velz: frandom(0, 2.5), startalphaf: 1, fadestepf: 0.035, sizestep: -1, startroll: frandom(0, 360), rollvel: random(0, 1) ? 2 : -2);
	}
	TNT1 A 0
	{
		A_Stop();
		if (teleloop <= 0)
		{
			A_StartSound("Apollyon/Teleport", 20, attenuation: ATTN_IDLE);
			A_QuakeEx(2, 2, 2, 40, 0, 800, "", QF_SCALEDOWN|QF_3D, rollintensity: 1);
			for (int i = 5; i > 0; i--) A_SpawnItemEx("DCY_Explosion_1", frandom(-self.radius, self.radius) / 4, frandom(-self.radius, self.radius) / 4, (frandom(self.height + 10, self.height - 10)) / 2, frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), flags: SXF_NOCHECKPOSITION);
			SetStateLabel("See");
		}
		teleloop--;
	}
	Loop;
  FireBlast:
	A_PB K 0
	{
		A_StartSound("Apollyon/Spell", CHAN_VOICE);
		for (int i = 0; i < 4; i++) A_SpawnItemEx(pillars[i], zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_SETMASTER);;
	}
	A_PB KLKLKLKLKLKLKLKLKLKLKLKL 1 A_FaceTarget();
	A_PB LKJ 4;
	A_PB J 0
	{
		A_QuakeEx(1, 1, 4, 50, 0, 1500, "", QF_SCALEDOWN|QF_3D, rollintensity: 0.75);
		A_StartSound("BigExplod", 15, attenuation: 0.75, pitch: 1.5);
		A_SpawnItemEx("DCY_Explosion_1_Biggie", 0, 0, 10, flags: SXF_NOCHECKPOSITION);
		
		A_Explode((50), 200, XF_NOTMISSILE);
		A_RadiusThrust(8000, 300, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE);
		A_RadiusThrust(180, 300, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE|RTF_THRUSTZ);
		
		for (int i = random(5, 6); i > 0; i--) A_SpawnItemEx("DCY_Explosion_1_Biggie", frandom(-self.radius * 10, self.radius * 10), frandom(-self.radius * 10, self.radius * 10), 10 + frandom(-10, 0));
		for (int i = random(5, 15); i > 0; i--) A_SpawnItemEx("DCY_NapalmBigFire", frandom(-self.radius * 7, self.radius * 7), frandom(-self.radius * 7, self.radius * 7), 1);
		for (int i = random(10, 20); i > 0; i--) A_SpawnItemEx("DCY_NapalmFire", frandom(-self.radius * 10, self.radius * 10), frandom(-self.radius * 10, self.radius * 10), 1);
	}
	A_PB IHGHIHGHIHGHIHGHIHGHIIHGHIHGHIHGHIHGHIHGHI 1;
	Goto See;
  Missile:
	A_PB A 0 A_JumpIf(target && Distance3D(target) < 200, "FireBlast");
	A_PB A 0 A_Jump(128, "Comet");
	Goto MissileContinue;
  MissileContinue:
	A_PB K 0 A_StartSound("Apollyon/Attack", CHAN_VOICE);
	A_P2 ABCABCAB 4 A_FaceTarget();
    A_P2 CCCC random(5, 20)
	{
		d = !d;
		
		A_FaceTarget(6, 6);
		A_SpawnProjectile("DCY_ApollyonTracer", 0, 40 * ((!d) ? -1 : 1), 0, CMF_AIMDIRECTION|CMF_TRACKOWNER);
	}
	A_P2 ABC 4;
	A_PB AAA 5;
    Goto See;
  Comet:
	A_PB GHIHIJKL 4 A_FaceTarget();
	A_PB M 4
	{
		A_SpawnProjectile("DCY_ApollyonComet", 36);
		A_FaceTarget();
	}
	A_PB NOP 4 A_FaceTarget();
	A_PB A 10 A_FaceTarget();
	Goto See;
  Pain:
    A_PB Q 5 A_RemoveChildren(1, RMVF_EVERYTHING);
    A_PB Q 5 A_Pain();
    Goto See;
  Death:
    A_PB Q 7
	{
		A_RemoveChildren(1, RMVF_EVERYTHING);
		DCY_Gibs(false);
	}
    A_PB R 7 A_Scream();
    A_PB S 7 A_NoBlocking();
    A_PB TUVW 7;
    A_PB XY 5;
    A_PB Z -1;
    Stop;
  }
}

Class DCY_ApollyonComet : DCY_GeneralComet
{
  Default
  {
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.71,1.01]";
  Radius 12;
  Height 12;
  Speed 20;
  +NEVERFAST
  +EXTREMEDEATH
  DamageType "Fire";
  Obituary "%o got turned into scarlet dust by Apollyon.";
  Damage 4;
  Scale 0.9;
  RenderStyle "Normal";
  Alpha 1.0;
  Projectile;
  Decal "Scorch";
  }
  
  override void Tick()
  {
	DCY_Projectile.Tick();
	
	if (died) return;
	if (vel ~== (0, 0, 0))
	{
		if (DCY_EffectDistance(900))
			for (int i = random(5, 10); i > 0; i--)
				A_SpawnItemEx("DCY_CometChunk", frandom(self.radius, -self.radius), frandom(self.radius, -self.radius), frandom(self.height, -self.height), frandom(-7, 7), frandom(-7, 7), frandom(-1, 10), frandom(0, 360));
		
		for (int i = random(4, 6); i > 0; i--)
		{
			A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_ApollyonFire", frandom(-50, 50), frandom(-50, 50), 0);
			A_SpawnItemEx("DCY_NapalmEmber", frandom(self.radius, -self.radius), frandom(self.radius, -self.radius), frandom(self.height, -self.height), frandom(-7, 7), frandom(-7, 7), frandom(-1, 10), frandom(0, 360));
		}
		
		A_SpawnItemEx("DCY_NapalmBigFire", flags: SXF_NOCHECKPOSITION);
		
		died = true;
	}
	else
	{
		roll += r;
		
		if ((level.time % random(1, 3)) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 100, 80, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 1), startalphaf: 0.25, fadestepf: 0.0014, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
	}
  }
}

Class DCY_ApollyonPillar : Actor
{
  Default
  {
  +NOINTERACTION
  +NOBLOCKMAP
  -SOLID
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Obituary "%o got turned into scarlet dust by Apollyon.";
  DamageType "Fire";
  DCY_ApollyonPillar.PillarDistance 580;
  DCY_ApollyonPillar.PillarAng 0;
  }
  
  double cangle;
  double dist1, savedist;
  property PillarDistance : dist1;
  property PillarAng : cangle;
  
  Mixin DCY_Effects;
  
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
		A_Warp(AAPTR_TARGET, dist1, 0, 5, cangle, WARPF_INTERPOLATE);
	
	if (abs(dist1) > 0.01) { dist1 *= 0.93; }
	else Destroy();
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	savedist = dist1;
  }
  
  States
  {
  Spawn:
	TNT1 A 1 NoDelay;
	TNT1 A 1
	{
		if (GetAge() >= 40) Destroy();
		cangle += 10;
		if (DCY_EffectDistance(1000))
		{
			for (int i = random(6, 9); i > 0; i--)
				A_SpawnParticleEx(0xff5219, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 20, 100, angle, frandom(-10, 10), frandom(-10, 10), frandom(2, 40), velz: frandom(5, 15), startalphaf: (1.0 - (dist1 / 580)) / 2, fadestepf: 0.1, sizestep: -5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 2 : -2);
		}
	}
	Goto Spawn+1;
  }
}

Class DCY_ApollyonPillar2 : DCY_ApollyonPillar { Default { DCY_ApollyonPillar.PillarAng 90; } }
Class DCY_ApollyonPillar3 : DCY_ApollyonPillar { Default { DCY_ApollyonPillar.PillarAng 180; } }
Class DCY_ApollyonPillar4 : DCY_ApollyonPillar  { Default { DCY_ApollyonPillar.PillarAng 270; } }

Class DCY_ApollyonTracer : DCY_Projectile
{
  Default
  {
  Radius 5;
  Height 5;
  Speed 25;
  ReactionTime 175;
  Damage 10;
  DamageType "Fire";
  RenderStyle "Add";
  Alpha 0.5;
  Projectile;
  +SEEKERMISSILE
  +FLOORHUGGER
  +NEVERFAST
  +THRUGHOST
  -NOGRAVITY
  Obituary "%o got turned into scarlet dust by Apollyon.";
  Seesound "Apollyon/Tracer";
  DeathSound "Apollyon/TracerExplode";
  Translation "0:255=%[0.09,0.00,0.00]:[1.72,0.79,0.28]";
  }
  
  int looping;
  
  States
  {
  Spawn:
    123E ABCDEFGHIJKLMN 1
	{
		if (looping >= 175)
			Destroy();
			
		if (looping < 60) A_Tracer2(1);
		looping++;
		
		let fire = Spawn("DCY_ApollyonFireTrail", pos);
		fire.scale = scale;
		fire.translation = translation;
		fire.frame = frame;
		
		for (int i = random(2, 4); i > 0; i--) A_SpawnParticleEx(0xff5219, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 20, random(50, 80), angle, frandom(-10, 10), frandom(-10, 10), frandom(2, 25), velz: frandom(0, 2.5), startalphaf: 1.0, fadestepf: 0.1, sizestep: -5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 2 : -2);
	}
    Loop;
  Death:
	FTRA K 3
	{
		for (int i = random(4, 6); i > 0; i--)
		{
			A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_ApollyonFire", frandom(-50, 50), frandom(-50, 50), 0);
		}
		
		A_SpawnItemEx("DCY_NapalmBigFire", flags: SXF_NOCHECKPOSITION);
	}
    FTRA L 2 A_Explode(20, 64);
    FTRA MNO 2;
    Stop;
  }
}

Class DCY_ApollyonFireTrail : DCY_Effect
{
  Default
  {
	RenderStyle "Add";
	Alpha 2;
	Scale 1;
	+NOINTERACTION
	-SOLID
	+NOBLOCKMAP
	Radius 0;
	Height 0;
  }
  
  States
  {
  Spawn:
	123E A 0;
	123E # 1
	{
		A_FadeOut(0.15);
		scale *= 0.9;
	}
	Wait;
  }
}