Class DCY_Transmuter : DCY_ShotgunZombe
{
  Default
  {
  Health 111;
  Translation "112:159=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "5:12=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "1:2=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "160:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]", "48:79=%[0.00,0.00,0.00]:[1.03,0.79,0.61]";
  Speed 4;
  FastSpeed 6;
  Tag "\c[Ultramarine]Transmuter\c-";
  DCYMonster.ExtraPrefix "\c[Ultramarine]";
  PainChance 90;
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +NODROPOFF
  +ALWAYSFAST
  +NEVERRESPAWN
  +DONTHARMCLASS
  BloodType "AndroidNeonFluid", "AndroidNeonFluid";
  SeeSound "transmuter/sight";
  PainSound "transmuter/pain";
  DeathSound "transmuter/death";
  ActiveSound "transmuter/active";
  DropItem "RocketAmmo";
  DropItem "RocketBox", 128;
  DCYMonster.GibType "DCY_GibletsAdd";
  }
  
  int torpedos;
  
  void A_TransmuterDodge(double speed)
  {
	A_ChangeVelocity(frandom(-speed, speed), random(-speed, speed), 3.5, CVF_REPLACE);
  }
  
  void A_TransmuterTorpedo()
  {
	A_StartSound("ROBOMISL", CHAN_WEAPON, 0, 1.0, 0.5);
	A_FaceTarget();
	A_SpawnProjectile("DCY_TransmuterTorpedo", 30, 0, 0);
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"Blue rocket insanity!",
		"%o fell before the might of the Transmuter.",
		"%o didn't see it coming."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  int missilecount;
  
  States
  {
  Spawn:
    RZ22 A 10 DCY_Look();
	Loop;
  Look:
	RZ22 AABBCCDD 4 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" G 10 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(2, "Dodge");
    RZ22 AABBCCDD 4 DCY_Chase();
	Loop;
  Dodge:
	RZ22 E 10 A_FaceTarget();
    RZ77 # 0
	{
		A_FaceTarget();
		frame = randompick(0, 1);
		A_TransmuterDodge(frandom(12, 16));
	}
	RZ77 ################# 1 { roll += (!frame) ? -22 : 22; }
	RZ22 E 20 { roll = 0; A_FaceTarget(); if (missilecount > 0) SetStateLabel("TorpedoStart"); }
	Goto See;
  DodgeM:
    RFR0 # 0 { missilecount = random(1, 3); }
	Goto Dodge;
  RareMissile:
    TNT1 A 0 A_Jump(95, "MissileReturn");
	Goto See;
  Missile:
	TNT1 A 0 
	{
		torpedos = 0;
		Return A_Jump(128, "Dodge");
	}
	Goto MissileReturn;
  MissileReturn:
	TNT1 A 0 A_Jump(128, "DodgeM");
	Goto Torpedo;
  Torpedo:
	RZ22 E 0 { missilecount = random(1, 3); }
	RZ22 EEEEEEEEEEEEEEEEEE 1 A_FaceTarget();
	Goto TorpedoStart;
  TorpedoStart:
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 EAAA 8;
	RZ22 E 0
	{
		missilecount--;
		if (missilecount < 0) SetStateLabel("See");
	}
	Loop;
  Pain:
    RZ22 G 3 { roll = 0; }
	RZ22 G 4 A_Pain();
	Goto See;
  Death:
    RZ22 H 1 { roll = 0; }
	RZ22 H 4 A_Scream();
	RZ22 H 1 A_NoBlocking();
	RZ22 IJKL 6;
	RZ22 L -1;
	Stop;
  XDeath:
    RZ22 M 1 { roll = 0; }
	RZ22 M 3 { DCY_Gibs(false); A_StartSound("Android/Gib", CHAN_VOICE); }
	RZ22 M 1 A_NoBlocking();
	RZ22 NOPQRSTU 5;
	RZ22 U -1;
	Stop;
  Raise:
    RZ22 LKJIH 6;
	Goto See;
  }
}