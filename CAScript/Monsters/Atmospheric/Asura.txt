Class DCY_Asura : DCY_CacoParent
{
  Default
  {
	Health 999;
	Tag "ASURA";
	DCYMonster.ScientificName "NO DATA";
	Species "Ambient";
	Mass 9999;
	Radius 30;
	Height 60;
	Speed 30;
	Monster;
	-FLOORCLIP
	-COUNTKILL
	+NODAMAGETHRUST
	+NORADIUSDMG
	+GHOST
	+DONTTHRUST
	+MISSILEMORE
	+FLOAT
	+NOGRAVITY
	+FLOATBOB
	+NOBLOOD
	+NOPAIN
	+PAINLESS
	+DONTFALL
	-NODAMAGE
	+NOINFIGHTING
	+DCYMONSTER.DCYBOSS
	+NOCLIP
	SeeSound "";
	DeathSound "";
	ActiveSound "";
	PainSound "";
	FloatBobStrength 0.05;
	Obituary "%o has faced depravity.";
	DamageFactor "Asura", 0;
  }
  
  override void Tick()
  {
	super.Tick();
	A_SpawnParticleEx(0x751b1b, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, random(90, 95), random(120, 280), frandom(0, 360), zoff: 30, velx: frandom(1, 5), vely: frandom(-0.5, 0.5), velz: frandom(-2.5, 2.5), accelz: 0.05, startalphaf: 0.35, fadestepf: 0.01, sizestep: 2, startroll: frandom(0, 360));
	A_SpawnParticleEx(0x873535, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 50, random(70, 100), angle, zoff: 30, startalphaf: 1, fadestepf: 0.1, sizestep: random(-1, -2), startroll: frandom(0, 360));
	A_AttachLight("AsuraLight", DynamicLight.PulseLight, "873535", 300, 350, DynamicLight.LF_DONTLIGHTSELF, ofs: (0, 0, 30));
	A_AttachLight("AsuraSLight", DynamicLight.PulseLight, "873535", 900, 950, DynamicLight.LF_SUBTRACTIVE|DynamicLight.LF_DONTLIGHTSELF, ofs: (0, 0, 30));
  }
  
  override void Die(Actor source, Actor inflictor, int dmgflags, name MeansOfDeath)
  {
	A_RemoveLight("AsuraLight");
	A_RemoveLight("AsuraSLight");
	Super.Die(source,inflictor,dmgflags,meansofdeath);
  }
  
  void A_AsuraChase()
  {
	A_FaceTarget(7, 7, 7, 7);
	A_Recoil(-1);
	vel.x += frandom(-2, 2);
	vel.y += frandom(-2, 2);
	vel.z += frandom(-1, 1);
	vel /= 1.123;
  }
  
  void A_AsuraDecide()
  {
	if (!random(0, 300)) { spaz = random(1, 6); SetStateLabel("Spaz"); }
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	A_StartSound("Asura/Hum", 400, CHANF_LOOPING, 0.75, 0.45, frandom(0.7, 1.2), startTime: frandom(0, 1));
  }
  
  int spaz;
  
  States
  {
  Spawn:
  Idle:
	NULL A 10 A_Look();
	Loop;
  See:
	NULL A random(10, 100);
	NULL A 1
	{
		if (target && CheckSight(target) && distance3D(target) < random(200, 500))
		{
			A_FaceTarget();
			SetStateLabel("Wake");
		}
	}
	Loop;
  Spaz:
	NULL A 0 A_Stop();
	NULL A 0 { for (int i = random(10, 60); i > 0; i--) A_Chase(null, null); }
	NULL A random(5, 60)
	{
		spaz--;
		if (spaz <= 0) SetStateLabel("Wake");
	}
	Loop;
  Wake:
	NULL A 1
	{
		A_AsuraChase();
		A_AsuraDecide();
		
		if (getAge() % 3 == 0) A_Explode(4, 60, XF_NOTMISSILE, damageType: "Asura");
	}
	Loop;
  Death:
	NULL A 20 A_FaceTarget();
	NULL A 0 { bNOCLIP = false; A_SetSize(0, 0); bNOINTERACTION = true; bTHRUACTORS = true; }
	NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		vel += (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10));
		A_Recoil(1);
		
		A_SpawnParticleEx(0x751b1b, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, random(90, 95), random(120, 280), frandom(0, 360), frandom(-1, 1) * (vel.x / 4), frandom(-1, 1) * (vel.y / 4), frandom(-1, 1) * (vel.z / 4) + 30, velx: frandom(1, 5), vely: frandom(-0.5, 0.5), velz: frandom(-2.5, 2.5), accelz: 0.05, startalphaf: 0.35, fadestepf: 0.01, sizestep: 2, startroll: frandom(0, 360));
		A_SpawnParticleEx(0x873535, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 50, random(70, 100), angle, frandom(-1, 1) * (vel.x / 4), frandom(-1, 1) * (vel.y / 4), frandom(-1, 1) * (vel.z / 4) + 30, startalphaf: 1, fadestepf: 0.1, sizestep: random(-1, -2), startroll: frandom(0, 360));
	}
	NULL A 0 A_StopSounds(0, 999);
	Stop;
  }
}