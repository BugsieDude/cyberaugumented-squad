Class DCY_CalamityTsar : DCY_KnightHell
{
  Default
  {
  Health 600;
  Translation "16:47=%[0.00,0.00,0.00]:[1.29,0.57,2.00]", "255:255=%[0.00,0.00,0.00]:[1.01,0.00,2.00]", "80:111=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "0:15=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "48:79=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "128:159=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "236:239=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "112:116=[255,255,255]:[255,255,128]", "117:121=[255,255,128]:[255,174,64]", "122:127=[255,169,53]:[81,40,0]", "160:235=%[0.00,0.00,0.00]:[2.00,1.37,0.50]", "240:254=%[0.00,0.00,0.00]:[2.00,1.37,0.50]";
  Tag "Calamity Tsar";
  Obituary "%o vanished before the Calamity Tsar.";
  HitObituary "%o got the slash of the Calamity Tsar.";
  +MISSILEMORE
  +DONTGIB
  +QUICKTORETALIATE
  PainChance 65;
  BloodColor "Yellow";
  SeeSound "Tsar/Sight";
  ActiveSound "Tsar/Active";
  PainSound "Tsar/Pain";
  DeathSound "Tsar/Death";
  DCYMonster.WalkFlames "DCY_YellowWalkFlames";
  DCYMonster.ScientificName "Brutis ductor";
  Speed 8;
  FastSpeed 10;
  }
  
  double dash;
  
  States
  {
  Spawn:
	HL59 A 10 DCY_Look();
	Loop;
  Look:
	HL59 AABBCCDD 3 { if (!random(0, 3)) A_WalkFlame(); DCY_LookingForPlayer(); }
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	HL59 AABBCCDD 3 { if (!random(0, 3)) A_WalkFlame(); DCY_Chase(); }
	Loop;
  Missile:
	HL59 A 0 A_Jump(25, "Ball");
	HL59 A 0 A_Jump(128, "Ball", "Tracers");
	HL59 A 0 A_Jump(70, "SuperDash");
	Goto Ball;
  Ball:
	HL59 EF 5 A_FaceTarget();
	HL59 G 5
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_TsarBall", 34, 0, frandom(-0.6, 0.6));
	}
	HL59 F 5;
	HL59 ST 5 A_FaceTarget();
	HL59 U 5
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_TsarBall", 34, 0, frandom(-0.6, 0.6));
	}
	HL59 T 5;
	Goto See;
  Tracers:
	HL59 K 0
	{
		//make sure the target is on ground
		if (target && target.pos.z >= self.pos.z) SetStateLabel("Ball");
	}
	HL59 K 1 A_StartSound(SeeSound, CHAN_VOICE);
	HL59 KKKKK 5 A_FaceTarget();
	HL59 EF 7 A_FaceTarget();
	HL59 G 5
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_TsarTracer");
	}
	HL59 F 5;
	HL59 ST 7 A_FaceTarget();
	HL59 U 5
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_TsarTracer");
	}
	HL59 T 5;
	Goto See;
  SuperDash:
	HL59 A 0
	{
		if (target && distance3D(target) < 500) SetStateLabel("Ball");
		dash = random(2, 7);
	}
	HL59 AAAA 7;
	HL59 A 0 A_StartSound("Tsar/Dash", CHAN_BODY);
	TNT1 AAAAAA 1
	{
		for (int i = 10; i > 0; i--)
		{
			A_Chase(null, null, CHF_NOPLAYACTIVE|CHF_NIGHTMAREFAST);
			A_SpawnItemEx("DCY_CalamityTsarSuperAura");
		}
	}
	TNT1 A 0
	{
		If (target && Distance3D(target) < 256) SetStateLabel("Ball");
		
		if (dash <= 0) SetStateLabel("See");
		dash--;
	}
	Goto SuperDash+6;
  Melee:
    HL59 EF 5 A_FaceTarget();
	HL59 G 5 A_CustomMeleeAttack(6 * random(1, 10), "knight/melee", "");
	HL59 F 5 A_FaceTarget();
	HL59 ST 5 A_FaceTarget();
	HL59 U 5 A_CustomMeleeAttack(6 * random(1, 10), "knight/melee", "");
	HL59 T 5 A_FaceTarget();
	Goto See;
  Pain:
	HL59 K 3;
	HL59 K 3 A_Pain();
	Goto See;
  Death:
	HL59 M 1;
	HL59 M 10 A_ScreamAndUnblock();
	HL59 NOPQ 4;
	HL59 R -1;
	Stop;
  Raise:
	HL59 QP 5;
	HL59 ONMK 3;
	Goto See;
  }
}

Class DCY_CalamityTsarSuperAura : Actor
{
  Default
  {
  RenderStyle "AddStencil";
  StencilColor "FF FF 99";
  Alpha 0.75;
  Scale 1.0;
  Radius 0;
  Height 0;
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  }
  
  States
  {
  Spawn:
	HL59 "#" 1
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.035, Scale.Y+0.035);
	}
	HL59 "#" 0 A_JumpIf(Alpha <= 0.0000000000000, "Stahp");
	Loop;
  Stahp:
	TNT1 A 1;
	Stop;
  }
}

Class DCY_TsarBall : DCY_BaronFireball
{
  Default
  {
  Damage 4;
  Speed 14.5;
  FastSpeed 22.5;
  +BRIGHT
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.01]";
  SeeSound "Tsar/Fire";
  DeathSound "Tsar/Impact";
  }
  
  vector3 v;
  vector3 savevel;
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	savevel = vel;
  }
  
  States
  {
  Spawn:
	NBKL AABBCC 1
	{
		A_Weave(1, 0, 1, 0);
		
		v.x += frandom(-1, 1);
		v.y += frandom(-1, 1);
		v.z += frandom(-1, 1);
		
		v.x = clamp(v.x, -2, 2);
		v.y = clamp(v.y, -2, 2);
		v.z = clamp(v.z, -2, 2);
		
		vel.x = savevel.x + v.x;
		vel.y = savevel.y + v.y;
		vel.z = savevel.z + v.z;
		
		A_SpawnFlames("d1983d", 15, 40, 0, false, frandom(-1.5, 1.5), thrust: 4);
	}
	Loop;
  Death:
	BAL7 C 0
	{
		A_SpawnItemEx("DCY_Explosion_1_Small", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnParticleEx(0xd1983d, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 120, 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.65, fadestepf: 0.0045, sizestep: 1.525);
		for (int i = 2; i > 0; i--) A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-3, 3) + 3, random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	BAL7 CCCDDDEEE 1
	{
		A_FadeOut(0.1, 0);
		A_SetScale(Scale.X+0.1, Scale.Y+0.1);
	}
	Stop;
  }
}

Class DCY_TsarTracer : DCY_Projectile
{
  Default
  {
  Seesound "Tsar/Ground";
  DeathSound "Apollyon/TracerExplode";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.01]";
  Radius 5;
  Height 5;
  Speed 5;
  Damage 8;
  XScale 0.95;
  YScale 1.2;
  DamageType "Fire";
  RenderStyle "Add";
  Alpha 1;
  Projectile;
  +SEEKERMISSILE
  +FLOORHUGGER
  +NEVERFAST
  +THRUGHOST
  -NOGRAVITY
  }
  
  int looping;
  
  States
  {
  Spawn:
    123E ABCDEFGHIJKLMN 1
	{
		if (looping >= 175)
			Destroy();
			
		if (looping < 20) A_Tracer2(5);
		if (looping > 20 && looping < 30) vel.xy *= 1.2;
		
		looping++;
		
		if (DCY_EffectDistance(sfxdistance))
		{
			let fire = Spawn("DCY_ApollyonFireTrail", pos);
			fire.scale = scale;
			fire.translation = translation;
			fire.frame = frame;
			for (int i = random(2, 4); i > 0; i--)
			{
				if (!random(0, 1)) A_SpawnParticleEx(0xffe18c, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 20, random(50, 80), angle, frandom(-10, 10), frandom(-10, 10), frandom(2, 25), velz: frandom(0, 2.5), startalphaf: 1.0, fadestepf: 0.05, sizestep: -3, startroll: frandom(0, 360), rollvel: random(0, 1) ? 2 : -2);
				for (int i = randompick(1, 1, 2); i > 0; i--) A_SpawnParticleEx(0xffe18c, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 70, frandom(1.00, 4.00), angle, frandom(-25, 25), frandom(-25, 25), frandom(-5, 25), (Vel.x / 4.75), (Vel.y / 4.75), (Vel.z / 4.75) + frandom(3, 10), startalphaf: 1.0, fadestepf: 0, sizestep: -0.15);
			}
		}
	}
    Loop;
  Death:
	FTRA K 3
	{
		for (int i = random(4, 6); i > 0; i--)
		{
			A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			A_SpawnItemEx("DCY_ApollyonFire", frandom(-50, 50), frandom(-50, 50), 0, flags: SXF_TRANSFERTRANSLATION, failchance: 128);
		}
		
		A_SpawnItemEx("DCY_NapalmBigFire", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    FTRA L 2 A_Explode(20, 64);
    FTRA MNO 2;
    Stop;
  }
}